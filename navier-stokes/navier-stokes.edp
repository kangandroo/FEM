load "medit"

border C1(t=0,2.2){x=t;y=0;label=1;}
border C2(t=0,0.41){x=2.2;y=t;label=2;}
border C3(t=0,2.2){x=2.2-t;y=0.41;label=3;}
border C4(t=0,0.41){x=0;y=0.41-t;label=4;}
border C5(t=0,2*pi){x=0.05*cos(t)+0.2; y=0.05*sin(t)+0.2;label=5;}
int n = 30;
mesh Th = buildmesh(C1(4*n)+C2(n)+C3(4*n)+C4(n)+C5(-n));

real H = 0.41;
func dt = 0.01;
func T = 2;
func ux = 12*y*(H-y)/(H^2);
func uy = 0.;
func nu = 0.0015;

fespace Uh(Th,P2); Uh u1,u2,v1,v2,uold1,uold2;
fespace Ph(Th,P1); Ph p,q;
Uh psi,phi;

problem NS (u1,u2,p,v1,v2,q,solver=Crout) =
    int2d(Th)(
        1/dt * (u1*v1 + u2*v2)
        + nu * ( dx(u1)*dx(v1) + dy(u1)*dy(v1)
        + dx(u2)*dx(v2) + dy(u2)*dy(v2))
        - p*q*(0.000001)
        - p*dx(v1) - p*dy(v2)
        - dx(u1)*q - dy(u2)*q
    )
    + int2d(Th) ( 
        - 1/dt*convect([uold1,uold2],-dt,uold1)*v1 
        - 1/dt*convect([uold1,uold2],-dt,uold2)*v2 
    )
    + on(1,3,4,u1=ux,u2=uy)
    + on(5,u1=0,u2=0); 

problem streamlines(psi,phi) =
    int2d(Th)( 
        dx(psi)*dx(phi) + dy(psi)*dy(phi)
    )
    + int2d(Th)( 
        -phi*(dy(u1)-dx(u2))
    )
    + on(1,2,3,4,5,psi=0);

int i = 0;
int imax = T/dt;
for(int i = 0; i <= imax; i++) {
    uold1=u1;
    uold2=u2;
    NS;
    streamlines;
    if(i%10 == 0) {
        plot(psi);
    }
}