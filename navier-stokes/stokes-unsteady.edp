load "medit"

border C1(t=0,4){x=t;y=0;label=1;}
border C2(t=0,1){x=4;y=t;label=2;}
border C3(t=0,4){x=4-t;y=1;label=3;}
border C4(t=0,1){x=0;y=1-t;label=4;}
border C5(t=0,2*pi){x=0.2*cos(t)+1; y=0.2*sin(t)+0.5;label=5;}
int n = 20;
mesh Th = buildmesh(C1(4*n)+C2(n)+C3(4*n)+C4(n)+C5(-n));

func dt = 0.01;
func T = 0.2;
func ux = y*(1-y);
func uy = 0;

fespace Uh(Th,P1); Uh u1,u2,v1,v2,uold1=0,uold2=0;
fespace Ph(Th,P1); Ph p,q;
Uh psi,phi;

solve Stokes (u1,u2,p,v1,v2,q,solver=Crout) =
    int2d(Th)( 
        ( dx(u1)*dx(v1) + dy(u1)*dy(v1)
        + dx(u2)*dx(v2) + dy(u2)*dy(v2) )
        - p*q*(0.000001)
        - p*dx(v1) - p*dy(v2)
        - dx(u1)*q - dy(u2)*q
    )
    + on(4,u1=ux,u2=uy)
    + on(1,2,3,5,u1=0,u2=0); 

problem StokesUnsteady (u1,u2,p,v1,v2,q,solver=Crout) =
    int2d(Th)(
        1/dt * (u1*v1 + u2*v2)
        + (dx(u1)*dx(v1) + dy(u1)*dy(v1)
        + dx(u2)*dx(v2) + dy(u2)*dy(v2))
        - p*q*(0.000001)
        - p*dx(v1) - p*dy(v2)
        - dx(u1)*q - dy(u2)*q
    )
    - int2d(Th)(1/dt * (uold1*v1 + uold2*v2))
    + on(4,u1=ux,u2=uy)
    + on(1,2,3,5,u1=0,u2=0); 

problem streamlines(psi,phi) =
    int2d(Th)( dx(psi)*dx(phi) + dy(psi)*dy(phi))
    + int2d(Th)( -phi*(dy(u1)-dx(u2)))
    + on(1,2,3,4,5,psi=0);
int i = 0;
for(real t = 0; t <= T; t += dt) {
    uold1=u1;
    uold2=u2;
    StokesUnsteady;
    streamlines;
    // savemesh(Th,"StokesUnsteadyU." + i + ".mesh");
    // savesol("StokesUnsteadyU." + i + ".sol", Th, [u1,u2,p]);
    // savemesh(Th,"StokesUnsteadyPsi." + i + ".mesh");
    // savesol("StokesUnsteadyPsi." + i + ".sol", Th, psi);
    i++;
}
plot(psi);